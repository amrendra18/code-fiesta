buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def isCiServer = (System.getenv().containsKey("TRAVIS"))
def API_KEY;
def API_USERNAME;
def RELEASE_STORE_PASSWORD
def RELEASE_KEY_ALIAS
def RELEASE_KEY_PASSWORD


if (isCiServer) {
    API_KEY = System.getenv("CLIST_API_KEY") as String;
    API_USERNAME = System.getenv("CLIST_API_USERNAME") as String;
    RELEASE_STORE_PASSWORD = System.getenv("RELEASE_STORE_PASSWORD") as String;
    RELEASE_KEY_ALIAS = System.getenv("RELEASE_KEY_ALIAS") as String;
    RELEASE_KEY_PASSWORD = System.getenv("RELEASE_KEY_PASSWORD") as String;
} else {
    API_KEY = '"' + codefiesta_api_key + '"';
    API_USERNAME = '"' + codefiesta_api_username + '"';
    RELEASE_STORE_PASSWORD = codefiesta_store_password;
    RELEASE_KEY_ALIAS = codefiesta_key_alias;
    RELEASE_KEY_PASSWORD = codefiesta_key_password;
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.amrendra.codefiesta"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 2
        versionName "1.1"
    }

    signingConfigs {
        release {
            storeFile file("config/codezera.jks")
            storePassword "$RELEASE_STORE_PASSWORD"
            keyAlias "$RELEASE_KEY_ALIAS"
            keyPassword "$RELEASE_KEY_PASSWORD"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        buildTypes.each {
            it.buildConfigField 'String', 'API_KEY', "$API_KEY"
            it.buildConfigField 'String', 'API_USERNAME', "$API_USERNAME"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'

    compile 'com.squareup:otto:1.3.8'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.github.bumptech.glide:glide:3.6.1'

    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
    compile 'io.reactivex:rxjava:1.0.13'

    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}

apply plugin: 'com.google.gms.google-services'
